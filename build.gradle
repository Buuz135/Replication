plugins {
    id 'net.neoforged.moddev' version '0.1.120'
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
}

group = 'com.buuz135'
version = '1.21.1-1.1.9'

java {
    archivesBaseName = 'Replication'
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

neoForge {
    version = "${neo_version}"

    parchment {
        minecraftVersion = '1.21'
        mappingsVersion = '2024.06.23'
    }

    validateAccessTransformers = true

    runs {
        client {
            client()
        }
        server {
            server()
        }
        data {
            data()
            programArguments.addAll '--mod', 'replication', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {

        titanium {
            sourceSet sourceSets.main
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name 'jared maven'
        url "https://maven.blamejared.com/"
    }

    maven { url = 'https://ldtteam.jfrog.io/ldtteam/modding' }
    maven {
        url = "https://www.cursemaven.com"
    }
}

dependencies {
    compileOnly "mezz.jei:jei-1.21.1-common-api:$jei_version"
    compileOnly "mezz.jei:jei-1.21.1-neoforge-api:$jei_version"
    runtimeOnly "mezz.jei:jei-1.21.1-neoforge:$jei_version"
    implementation project.dependencies.create('com.hrznstudio:titanium:1.21-4.0.20')
    implementation "curse.maven:jade-324717:5444008"
    compileOnly "curse.maven:the-one-probe-245211:5475189"
    runtimeOnly "curse.maven:industrial-foregoing-266515:6030556"
    runtimeOnly "curse.maven:dark-mode-everywhere-574123:5922655"

}

jar {
    manifest {
        attributes(["Specification-Title"     : project.name,
                    "Specification-Vendor"    : "Buuz135, Rid",
                    "Specification-Version"   : "1.0",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${project.version}",
                    "Implementation-Vendor"   : "Buuz135, Rid",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    "ModSide"                 : "BOTH"
        ])
    }
}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    options.compilerArgs << '-Xmaxerrs' << '2000'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name 'repo'
            url(file('repo'))
        }
    }
}
// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
